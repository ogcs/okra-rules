/*
 *
 *
 *          Copyright (c) 2021. - TinyZ.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
ext.root = project

ext.version_options = [
        SPRING_BOOT_VERSION: "2.2.5.RELEASE",
        SPRING_VERSION     : "5.2.4.RELEASE",
        JETTY_VERSION      : "9.4.16.v20140903",
        JUNIT_VERSION      : "5.5.1",
        MOCKITO_VERSION    : "2.25.0"
]
ext.pack_version = (hasProperty("pack_version") ? property("pack_version") : version)

group 'org.ogcs'
version '1.0.2.Beta-SNAPSHOT'
description = 'Okra rule engine implement.'

ext.publish_jvm_projects = [
        ':rules-all',
        ':rules-core',
        ':rules-spring'
]

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'distribution'

    sourceCompatibility = 11
    targetCompatibility = 11
    compileJava.options.setEncoding("UTF-8")
    compileTestJava.options.setEncoding("UTF-8")

    group = "${rootProject.group}"
    version = "${rootProject.version}"

    dependencies {
        //     slf4j
        implementation("org.apache.logging.log4j:log4j-slf4j-impl:2.9.1")
        implementation("org.apache.logging.log4j:log4j-api:2.9.1")
        implementation("org.apache.logging.log4j:log4j-core:2.9.1")
        implementation("org.slf4j:slf4j-api:1.7.25")
        implementation("org.slf4j:jcl-over-slf4j:1.7.25")

        testImplementation("org.junit.jupiter:junit-jupiter:${version_options.JUNIT_VERSION}")
        testImplementation("org.mockito:mockito-core:${version_options.MOCKITO_VERSION}")
    }

    repositories {
        mavenLocal()
        maven { url "https://repo1.maven.org/maven2/" }
        maven { url "https://repo.maven.apache.org/maven2/" }
        maven { url "https://repo.typesafe.com/typesafe/repo" }
        maven { url "https://maven.aliyun.com/repository/central" }
        maven { url "https://maven.aliyun.com/repository/spring" }
    }

    test {
        ignoreFailures = true
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

//  <editor-fold desc="   Maven Publish  Local ">
//  https://docs.gradle.org/current/userguide/publishing_maven.html#publishing_maven:install

Closure init_publish_module = {

    apply plugin: 'maven-publish'

    task generatePomPropertiesFile {
        inputs.properties([
                'version'   : "${-> project.version}",
                'groupId'   : "${-> project.group}",
                'artifactId': "${-> project.name}"
        ])
        outputs.file "$project.buildDir/generatePomPropertiesFile/pom.properties"
        doLast {
            File file = outputs.files.singleFile
            if (file.isFile() && file.exists()) {
                file.delete()
            }
            file.parentFile.mkdirs()
            file << inputs.properties.collect { key, value -> "$key: $value" }.join('\n')
        }
    }

    model {
        tasks.jar {
            manifest {
                attributes 'Implementation-Title': project.description ?: rootProject.description,
                        'Implementation-Version': project.version,
                        'Built-By': System.getProperty('user.name'),
                        'Built-Date': new Date(),
                        'Built-JDK': System.getProperty('java.version'),
                        'Built-Gradle': gradle.gradleVersion,
                        'Target-JDK': project.targetCompatibility
            }
            into("META-INF/maven/$project.group/$project.name") {
                from { generatePomFileForMavenPublication }
                rename ".*.xml", "pom.xml"
                from generatePomPropertiesFile
            }
        }
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        archiveClassifier.set('sources')
    }

    task javadocJar(type: Jar) {
        from javadoc {
            options.encoding = 'UTF-8'
            options.tags = ["date"]
            failOnError = false
        }
        archiveClassifier.set('javadoc')
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = "$project.name"
                    description = "$project.description"
                    url = "https://github.com/ogcs/okra-rules"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = "TinyZ"
                            name = "TinyZzh"
                            email = "tinyzzh815@gmail.com"
                        }
                    }
                }
            }
        }
        repositories {
            mavenLocal()
            maven {
                name "local_nexus"
                url project.findProperty("mavenRepoUrl") ?: ""
                credentials {
                    username = project.findProperty("mavenRepoUser") ?: ""
                    password = project.findProperty("mavenRepoPassword") ?: ""
                }
                allowInsecureProtocol = true
            }
            maven {
                name "gitea"
                url = uri("http://192.168.190.182:3000/api/packages/zzh/maven")
                credentials(HttpHeaderCredentials) {
                    name = "Authorization"
                    value = "token " + project.findProperty("giteaMavenAccessToken")
                }
                authentication {
                    header(HttpHeaderAuthentication)
                }
                allowInsecureProtocol = true
            }
        }
    }
}
ext.publish_jvm_projects.each {
    project(it, init_publish_module)
}
/// </editor-fold>

/// <editor-fold desc="  Code Coverage  ">

ext.codecovProjects = subprojects.findAll {
    return ["rules-core", "rules-spring"].contains(it.name)
}
configure(codecovProjects) {
    apply plugin: 'jacoco'

    jacoco {
        toolVersion = "0.8.6"
    }
}
task codeCoverageReport(type: JacocoReport) {
    getExecutionData().setFrom(fileTree(dir: project.rootDir.absolutePath).include("**/build/jacoco/*.exec"))

    project.ext.codecovProjects.each {
        sourceSets it.sourceSets.main
    }

    reports {
//        xml.enabled false
//        xml.destination file("${buildDir}/reports/report.xml")
//        html.enabled false
//        html.destination file("${buildDir}/reports/html")
//        csv.enabled false
    }

    afterEvaluate {
        getClassDirectories().setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: [
                            '**/bootstrap/**'
                    ])
        }))
    }

    //  jacocoRootReport doesn't work if some subprojects don't have any tests at all because this causes the onlyIf of JacocoReport to be false.
    onlyIf = { true }
}
codeCoverageReport.dependsOn {
    ext.codecovProjects*.test
}
/// </editor-fold>